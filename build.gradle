buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.1.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

repositories {
    maven { //JEI, IntWheel, McJtyLib, XNet, RFTools, TOP, OpenComputers & ElecCore
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven { // Waila
        name "Mobius Repo"
        url "https://tehnut.info/maven"
    }
    maven { //RF
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven { //Tesla
        name = "TESLA"
        url = "https://maven.mcmoddev.com"
    }
}

def ENV = System.getenv()
if (ENV.DRONE_BUILD_NUMBER) {
    version += ".n" + ENV.DRONE_BUILD_NUMBER
}
group= "mcjty.deepresonance" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "deepresonance"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = mcp_mappings
    useDepAts = true
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

dependencies {
    deobfCompile "mezz.jei:jei_${jei_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${waila_version}"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-${top_version}"
    deobfCompile "cofh:RedstoneFlux:${redstoneflux_version}"
    deobfCompile "net.darkhax.tesla:Tesla-${tesla_version}"

    deobfCompile "com.github.mcjty:intwheel:${intwheel_version}"
    if (!project.hasProperty("singleproject")) {
        deobfCompile "com.github.mcjty:mcjtylib:${mcjtylib_version}"
        deobfCompile "com.github.mcjty:xnet:1.12-1.8.0"
        deobfCompile "com.github.mcjty:rftools:1.12-7.70"
    } else {
	    compile project(':McJtyLib')
	    compile project(':XNet')
	    compile project(':RFTools')
    }
    compile "li.cil.oc:OpenComputers:${oc_version}"
    deobfCompile "nl.elec332.core:ElecCore:1.12.2-1.9.452"
}

jar {
    manifest {
        attributes 'FMLAT': 'deepresonance_at.cfg'
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "${modname} ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
                temp = temp.replaceAll("(\\S+\\/\\S+)#([0-9]+)\\b", "<a href=\"https://github.com/\$1/issues/\$2\">\$0</a>");
                temp = temp.replaceAll("#([0-9]+)\\b(?!<\\/a>)", "<a href=\"https://github.com/$github_project/issues/\$1\">\$0</a>");
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}

tasks.curseforge.enabled = project.hasProperty('curseforge_key')

curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.projectId
        changelog = System.getenv('CHANGELOG') == null || System.getenv('CHANGELOG').equals('none') ? getChangelogText() : System.getenv('CHANGELOG')
        changelogType = 'html'
        releaseType = project.curse_type
        addGameVersion '1.12.2'
        mainArtifact(jar) {
            displayName = "${modname} - ${version}"
        }
        relations {
            requiredLibrary 'mcjtylib'
            requiredLibrary 'eleccore'
            optionalLibrary 'rftools'
            optionalLibrary 'redstone-flux'
            optionalLibrary 'the-one-probe'
            optionalLibrary 'rftools-control'
        }
    }
}
